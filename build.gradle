plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.11'
	id 'io.spring.dependency-management' version '1.1.4'

	// asciidoctor
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'sopt.org'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

jar {
	enabled = false
}

configurations {
	asciidoctorExt  // asciidoctorExt
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	// JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// MySQL
	runtimeOnly 'com.mysql:mysql-connector-j'

	// H2 (테스트용)
	testImplementation 'com.h2database:h2'

	// Security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// Web
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// Spring Validatior
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Spring Scheduler Test
	testImplementation 'org.awaitility:awaitility:4.2.0'

	// Slack Webhook
	implementation 'com.slack.api:slack-api-client:1.28.0'
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'com.squareup.okhttp3:okhttp:4.10.0'
	implementation 'com.slack.api:slack-app-backend:1.28.0'
	implementation 'com.slack.api:slack-api-model:1.28.0'

	// Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Rest-docs
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	dependsOn test

	baseDirFollowsSourceFile()
}

asciidoctor.doFirst {
	delete file('src/main/resources/static/docs')
}

// build하여 생성된 html 파일을 src/main/resources/static/docs로 복사
task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}

tasks.named('test') {
	useJUnitPlatform()
}
